buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:0.6"
    classpath "org.gradle.api.plugins:gradle-nexus-plugin:0.7.1"
  }
}
apply plugin: "com.jfrog.bintray"
apply plugin: "groovy"
apply plugin: "nexus"
apply from: "$rootDir/gradle/credentials.gradle"
apply from: "$rootDir/gradle/integration-test.gradle"

group = "com.github.robfletcher"
version = "2.0.2-SNAPSHOT"
archivesBaseName = "compass-gradle-plugin"

repositories {
  jcenter()
}

dependencies {
  compile gradleApi()
  compile localGroovy()
  compile "com.github.jruby-gradle:jruby-gradle-plugin:0.1.5"

  testCompile("org.spockframework:spock-core:0.7-groovy-2.0") {
    exclude group: "org.codehaus.groovy"
  }
  testCompile "net.sourceforge.cssparser:cssparser:0.9.14"
}

task sourceJar(type: Jar) {
  from sourceSets.main.allSource
}

install {
  repositories {
    mavenDeployer {
      repository url: "file://${buildDir.absolutePath}/localRepo"
    }
  }
}

bintray {
  user = project.bintrayUsername
  key = project.bintrayApiKey
  publish = true
  dryRun = false
  configurations = ["archives"]

  pkg {
    userOrg = "robfletcher"
    repo = "gradle-plugins"
    name = "compass-gradle-plugin"
    licenses = ["Apache-2.0"]
    labels = ["compass"]
    desc = "Gradle plugin for Sass & Compass compilation"

    version {
      name = project.version
      vcsTag = "v${project.version}"
      attributes = ["gradle-plugin": "com.github.robfletcher.compass:com.github.robfletcher:compass-gradle-plugin"]
    }
  }
}
bintrayUpload.dependsOn assemble
